(in-package #:yamson)

(defparser yaml-block-sequence-element (level)
  (yaml-indicator '#\-)
  (yaml-value level))

(defparser yaml-block-sequence (level)
  (for ((elems (repsep (yaml-block-sequence-element level) (yaml-newline-indent level level) 1)))
    (construct-sequence elems)))

(defparser yaml-block-complex-key (level)
  (yaml-indicator '#\?)
  (yaml-value level))

(defparser yaml-block-mapping-element-value (level)
  (or (progn (yaml-newline-indent level level) (yaml-block-sequence level))
      (yaml-value level)))

(defparser yaml-block-mapping-element (level)
  (for ((element (or (cons (yaml-block-complex-key level)
                           (or (progn (yaml-newline-indent level level)
                                      (yaml-indicator '#\:)
                                      (yaml-block-mapping-element-value level))
                               (constantly (construct-null))))
                     (cons (or (yaml-value-simple) (yaml-string-unquoted))
                           (progn (yaml-whitespaces) (yaml-indicator '#\:) (yaml-block-mapping-element-value level))))))
    (cons (car element) (cdr element))))

(defparser yaml-block-mapping (level)
  (for ((fields (repsep (yaml-block-mapping-element level) (yaml-newline-indent level level) 1)))
    (construct-mapping fields)))

(defparser yaml-flow-whitespaces ()
  (rep (yaml-newline) 0)
  (yaml-whitespaces))

(defparser yaml-flow-trim (parser)
  (prog2 (yaml-flow-whitespaces) parser (yaml-flow-whitespaces)))

(defparser yaml-flow-object-terminator ()
  (or (yaml-flow-brackets) '#\, (yaml-indicator '#\:)))

(defparser yaml-flow-string-unquoted ()
  (yaml-string-unquoted (yaml-flow-object-terminator)))

(defparser yaml-flow-value (&optional value-required-p)
  (yaml-flow-whitespaces)
  (or (let ((anchor (yaml-anchor)))
        (for ((value (yaml-flow-value t)))
          (setf (gethash anchor (yaml-anchors)) value)))
      (let ((tag (yaml-tag)))
        (for ((value (yaml-flow-value t)))
          (yaml-tag-process tag value (yaml-tags))))
      (or (prog1 (yaml-value-unquoted)
            (yaml-flow-whitespaces)
            (peek (yaml-flow-object-terminator)))
          (prog1 (or (yaml-value-quoted) (yaml-flow-string-unquoted))
            (yaml-flow-whitespaces))
          (prog1 (constantly (construct-null))
            (yaml-flow-whitespaces)
            (rep (not (yaml-flow-brackets)) (if value-required-p 0 1) 1)))))

(defparser yaml-flow-complex-key ()
  (yaml-indicator '#\?)
  (yaml-flow-whitespaces)
  (or (prog1 (yaml-flow-value) (or (peek (yaml-flow-object-terminator)) (peek '#\:)))
      (yaml-string-unquoted-multiline 0)))

(defparser yaml-flow-mapping-element (&optional sequence-element-p)
  (yaml-flow-whitespaces)
  (for ((element (or (cons (yaml-flow-complex-key)
                           (or (progn '#\: (yaml-flow-value t)) (constantly (construct-null))))
                     (cons (yaml-flow-value)
                           (or (progn '#\: (yaml-flow-value t)) (constantly #1='#:null))))))
    (if (eq (cdr element) #1#)
        (if sequence-element-p (car element) (cons (car element) (construct-null)))
        (cons (car element) (cdr element)))))

(defparser yaml-flow-sequence ()
  (for ((list (prog2 '#\[
                  (repsep (yaml-flow-mapping-element t) '#\,)
                (opt '#\,)
                (yaml-flow-whitespaces)
                (cut '#\]))))
    (construct-sequence list)))

(defparser yaml-flow-mapping ()
  (for ((alist (prog2 '#\{
                   (repsep (yaml-flow-mapping-element nil) '#\,)
                 (opt '#\,)
                 (yaml-flow-whitespaces)
                 (cut '#\}))))
    (construct-mapping alist)))

(defparser yaml-value-unquoted ()
  (or (yaml-boolean) (yaml-number) (yaml-null)))

(defparser yaml-value-quoted ()
  (or (yaml-string-single-quoted) (yaml-string-double-quoted)
      (yaml-flow-sequence) (yaml-flow-mapping)
      (yaml-alias)))

(defparser yaml-value-simple ()
  (or (yaml-value-unquoted) (yaml-value-quoted)))

(defparser yaml-value (parent-level)
  (let ((level (constantly (1+ parent-level))))
    (declare (type non-negative-fixnum level))
    (let ((child-level (yaml-mixed-indent level)))
      (declare (type non-negative-fixnum child-level))
      (or (let ((anchor (yaml-anchor)))
            (for ((value (yaml-value parent-level)))
              (setf (gethash anchor (yaml-anchors)) value)))
          (let ((tag (yaml-tag)))
            (for ((value (yaml-value parent-level)))
              (yaml-tag-process tag value (yaml-tags))))
          (or (prog1 (yaml-value-simple) (peek (yaml-end-of-value level)))
              (yaml-string-multiline level)
              (yaml-block-sequence child-level) (yaml-block-mapping child-level)
              (yaml-string-unquoted-multiline level) (constantly (construct-null)))))))
